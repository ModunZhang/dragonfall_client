apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    lintOptions {
        warningsAsErrors false
        disable 'CommitPrefEdits'
    }
    defaultConfig {
        applicationId "com.xapcna.dragonfall"
        minSdkVersion 15
        targetSdkVersion 15
        versionCode 112
        versionName "1.1.3"
    }

    productFlavors {
        googleplay {
            versionName "GooglePlay"
            proguardFile  'proguard-rules.pro'
        }
        paypal {
            versionName "PayPal"
            proguardFile  'proguard-rules.pro' //TODO: set the proguardFile
        }
    }

    sourceSets.main {
        java.srcDir "src"
        res.srcDir "res"
        jniLibs.srcDir "libs"
        manifest.srcFile "AndroidManifestMain.xml"
        assets.srcDir "assets"
        aidl.srcDir "src/com/android/vending/billing" //google play v3 billing
    }

    sourceSets.googleplay {
        manifest.srcFile "AndroidManifest.xml"

    }

    sourceSets.paypal {
        manifest.srcFile "AndroidManifestPayPal.xml"
    }


    signingConfigs {
        release {
            storeFile file("keystore/xapcn_dragonfall_gp.keystore")
            storePassword "twhsMbcZxJ4isvgWX4BJ"
            keyAlias "xapcn_dragonfall"
            keyPassword "twhsMbcZxJ4isvgWX4BJ"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    //TODO: set the jar to each flavors
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':libcocos2dx')
    //google play services
//    compile ('com.google.android.gms:play-services:8.1.0')
    googleplayCompile 'com.google.android.gms:play-services-plus:8.1.0'
    googleplayCompile 'com.google.android.gms:play-services-ads:8.1.0'
    googleplayCompile 'com.google.android.gms:play-services-gcm:8.1.0'
    googleplayCompile 'com.google.android.gms:play-services-vision:8.1.0'
    //facebook
    compile 'com.facebook.android:facebook-android-sdk:4.0.0'
}

//dannyHe : We want to use custom ant antenna task for different flavors.

ant.importBuild '../GooglePlayPreprocess.xml'
ant.importBuild '../PayPalPreprocess.xml'

android.applicationVariants.all { variant ->
    variant.productFlavors.each { flavor ->
        if (flavor.name.equals('googleplay')) {
            variant.javaCompile.dependsOn(GooglePlayPreprocess)
        }
        else if(flavor.name.equals('paypal')){
            variant.javaCompile.dependsOn(PayPalPreprocess)
        }
    }
}

//end for antenna